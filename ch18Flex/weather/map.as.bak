// Fig. 18.18 : map.as
// Handles the map functions for the Weather demo
import mx.managers.CursorManager;
import flash.utils.getTimer;
import flash.utils.Timer;
import com.yahoo.webapis.maps.methodgroups.*;
import mx.controls.Alert;

   // begin define constants
   private const swfDomID:String = "weather";   
   private const UUID:int = getTimer();

   // YahooAPIKey string must be your own from developer.yahoo.com
   private const YahooAPIKey:String = "YOUR_YAHOO_ID_HERE";
   private const mapURL:String = "as2map.swf";
   private var mapController:MapController;
   
   private function loadMap():void
   {
      CursorManager.setBusyCursor(); // turn on busy cursor

      // wait 2 seconds, then call initMap method
      var delayWhileLoading:Timer = new Timer( 2000, 1 );
      delayWhileLoading.addEventListener( "timer", initMap );
      delayWhileLoading.start();
   } // end function loadMap
   
   // Handles map after it has loaded
   public function initMap( event:TimerEvent ):void 
   {     
      // instantiate map controller
      mapController = new MapController( yahooMap ); 
         
      // instantiate panTool method
      var panTools:PanTool = new PanTool( yahooMap ); 

      // allow user to pan accross the map
      panTools.setPanTool( true ); 
      
      // instantiate the maps widget class
      var widgets:Widgets = new Widgets( yahooMap ); 
         
      // activate Navigator widget
      widgets.showNavigatorWidget(); 
         
      // activate map type selection widget
      widgets.showSatelliteControlWidget(); 

      // turn off busy cursor
      CursorManager.removeBusyCursor();
   }  // end function initMap